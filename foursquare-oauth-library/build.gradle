apply plugin: 'com.android.library'
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    compileSdkVersion 17
    buildToolsVersion "21.1.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: mavenUser, password: mavenPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: mavenUser, password: mavenPassword)
            }
            pom.project {

                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }


                groupId 'com.foursquare'
                artifactId 'foursquare-android-nativeoauth-lib'
                version '1.0.1-SNAPSHOT'

                packaging 'apklib'

                name 'Foursquare native OAuth for Android'
                description 'Foursquare native authentication library makes it easier for your app\'s users to connect with Foursquare.'
                url 'https://github.com/foursquare/foursquare-android-oauth'

                scm {
                    url 'https://github.com/foursquare/foursquare-android-oauth'
                    connection 'scm:git:git://github.com/foursquare/foursquare-android-oauth.git'
                    developerConnection 'scm:git:git@github.com:foursquare/foursquare-android-oauth.git'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'foursquare'
                        name 'Foursquare'
                    }
                }

                issueManagement {
                    system 'GitHub Issues'
                    url 'https://github.com/foursquare/foursquare-android-oauth/issues'
                }

                dependencies {
                    dependency {
                        groupId 'com.google.android'
                        artifactId 'android'
                        version '4.1.1.4'
                    }
                    dependency {
                        groupId 'com.google.android'
                        artifactId 'support-v4'
                        version 'r7'
                    }
                }
            }
        }
    }
}